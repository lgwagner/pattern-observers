node globally_a_responds_to_b_within_n(a : bool; b : bool; n : int) returns (holds : bool);
var
   state,pre_state : subrange [-1,2] of int;
   latch_n : int;
   i,pre_i : int;
   prop1 : bool;
let
   latch_n = n -> pre latch_n;

   pre_i = 0 -> pre i;
   
   i = 
      if (pre_state = 0)
      then 
         if (b and not a)
         then 0
         else pre_i
      else if (pre_state = 1)
      then
         if (not a)
         then pre_i + 1
         else if (a and b)
         then 0
         else pre_i
      else if (pre_state = 2)
      then pre_i
      else pre_i;
   
   pre_state = 0 -> pre state;

   state = 
      if (pre_state = 0)
      then
         if (not b)
         then 0
         else if (a and b)
         then 0
         else if (not a and b)
         then 1
         else -1
      else if (pre_state = 1)
      then
         if (a and b)
         then 1
         else if (not a and (i < latch_n))
         then 1
         else if (not a and (i >= latch_n))
         then 2
         else if (a and not b)
         then 0
         else -1
      else if (pre_state = 2)
      then 2
      else -1;
      
   holds = (state <> 2);
   prop1 = (state <> -1);
   --%PROPERTY prop1;
tel;
